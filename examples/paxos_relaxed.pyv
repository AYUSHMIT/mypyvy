sort node
sort value
sort quorum
sort round

immutable constant none: round
immutable relation le(round, round)

# instantiate total_order(round, le)
axiom le(X,X)
axiom le(X, Y) & le(Y, Z) -> le(X, Z)
axiom le(X, Y) & le(Y, X) -> X = Y
axiom le(X, Y) | le(Y, X)

immutable relation member(node, quorum)
axiom forall Q1, Q2. exists N. member(N, Q1) & member(N, Q2)

mutable relation one_a(round)
mutable relation one_b(node, round)
mutable relation left_round(node, round)
mutable relation proposal(round, value)
mutable relation vote(node, round, value)
mutable relation decision(round, value)

mutable relation node_active(node)
mutable relation value_active(value)
mutable relation quorum_active(quorum)
mutable relation round_active(round)

# WLOG, the active domain includes everything in the inital state
init node_active(N)
init value_active(V)
init quorum_active(Q)
init round_active(R)

init !one_a(R)
init !one_b(N, R)
init !left_round(N, R)
init !proposal(R, V)
init !vote(N, R, V)
init !decision(R, V)

transition send_1a(r: round)
  modifies one_a
  old(round_active(r)) & 
  r != none &
  (forall R. one_a(R) <-> old(one_a(R)) | R = r)

transition join_round(n: node, r: round)
  modifies one_b, left_round
  old(node_active(n)) & 
  old(round_active(r)) & 
  r != none &
  old(one_a(r)) &
  !old(left_round(n, r)) &
  (forall N, R. one_b(N, R) <-> old(one_b(N,R)) | N = n & R = r) &
  (forall N, R. left_round(N, R) <-> old(left_round(N, R)) | N = n & !le(r, R))

transition propose(r: round, q: quorum, maxr: round, v: value)
  modifies proposal
  old(round_active(r)) &
  old(quorum_active(q)) &
  old(round_active(maxr)) &
  old(value_active(v)) & 
  r != none &
  (forall V. old(value_active(V)) -> !old(proposal(r, V))) &
  (forall N. old(node_active(N)) & member(N, q) -> old(one_b(N, r))) &
  ((maxr = none & forall N,MAXR,V. old(node_active(N)) & old(round_active(MAXR)) & old(value_active(V)) -> !(member(N, q) & !le(r,MAXR) & old(vote(N,MAXR,V)))) |
   (maxr != none &
    (exists N. old(node_active(N)) & member(N, q) & !le(r,maxr) & old(vote(N,maxr,v))) &
    (forall N,MAXR,V. old(node_active(N)) & old(round_active(MAXR)) & old(value_active(V)) & (member(N, q) & !le(r,MAXR) & old(vote(N,MAXR,V))) -> le(MAXR,maxr)))) &
  (forall R, V. proposal(R, V) <-> old(proposal(R, V)) | (R = r & V = v))

transition cast_vote(n: node, v: value, r: round)
  modifies vote
  old(node_active(n)) &
  old(value_active(v)) &
  old(round_active(r)) & 
  r != none &
  !old(left_round(n, r)) &
  old(proposal(r, v)) &
  (forall N, R, V. vote(N, R, V) <-> old(vote(N, R, V)) | N = n & R = r & V = v)

transition decide(r: round, v: value, q: quorum)
  modifies decision
  old(round_active(r)) & 
  old(value_active(v)) &
  old(quorum_active(q)) &
  r != none &
  (forall N. old(node_active(N)) & member(N, q) -> old(vote(N, r, v))) &
  (forall R, V. decision(R, V) <-> old(decision(R, V)) | R = r & V = v)

transition decrease_domain()
  modifies node_active, value_active, quorum_active, round_active
  (forall N. node_active(N) -> old(node_active(N))) &
  (forall V. value_active(V) -> old(value_active(V))) &
  (forall Q. quorum_active(Q) -> old(quorum_active(Q))) &
  (forall R. round_active(R) -> old(round_active(R)))

definition unique_decisions() =
  decision(R1, V1) & decision(R2, V2) -> V1 = V2

safety unique_decisions


#  invariant proposal(R,V1) & proposal(R,V2) -> V1 = V2
#  invariant vote(N,R,V) -> proposal(R,V)
#  invariant !vote(N,none,V)
#  invariant one_b(N,R2) & !le(R2,R1) -> left_round(N,R1)
#  
#  #invariant
#  #   forall R1, V1, R2, V2.
#  #     (exists Q. forall N. member(N, Q) -> !left_round(N, R1) | vote(N, R1, V1)) &
#  #     !le(R2, R1) &
#  #     proposal(R2, V2) ->
#  #     V1 = V2
#  
#  #invariant forall R, V, Q.
#  #  choosable(R, V, Q) <->
#  #  (forall N. member(N, Q) -> !left_round(N, R) | vote(N, R, V))
#  
#  invariant
#    forall R1, V1, R2, V2, Q.
#      choosable(R1, V1, Q) ->
#      !le(R2, R1) &
#      proposal(R2, V2) ->
#      V1 = V2
#  
#  
#  invariant
#    forall R, V.
#      decision(R, V) ->
#      forall N. member(N, decision_quorum(R, V)) -> vote(N, R, V)
#  

sat trace {
  send_1a
  join_round
  propose
  cast_vote
  decide

  decrease_domain

  send_1a
  join_round
  propose
  cast_vote
  decide
  assert !unique_decisions
}
