sort node

immutable relation internal(node)

mutable relation sent(node, node)
mutable relation allowed_in(node)

mutable relation aborted()
mutable relation witness_notif(node, node)

init !sent(S,D)
init !allowed_in(N)
init !aborted
init !witness_notif(I, E)

transition send_from_internal(src: node, dst: node)
  modifies sent, allowed_in, witness_notif
  & old(internal(src))
  & !old(internal(dst))
  & (forall S,D. sent(S,D) <-> old(sent(S,D)) | S = src & D = dst)
  & (forall N. allowed_in(N) <-> old(allowed_in(N)) | N = dst)
  & (forall I,E. witness_notif(I,E) <-> old(witness_notif(I,E)) | I = src & E = dst)

transition send_to_internal(src: node, dst: node)
  modifies sent
  & !old(internal(src))
  & old(internal(dst))
  & old(allowed_in(src))
  & (forall S,D. sent(S,D) <-> old(sent(S,D)) | S = src & D = dst)

transition discover_violation(s: node, d: node)
  modifies aborted
  & old(sent(s, d))
  & old(internal(d))
  & (forall I. !old(witness_notif(I, s)))
  & aborted

safety !aborted

# safety sent(S,D) & internal(D) -> exists I. internal(I) & sent(I,S)

invariant allowed_in(D) -> exists I. internal(I) & sent(I,D)
invariant internal(I) & sent(I, S) -> witness_notif(I, S)
invariant sent(S,D) & internal(D) -> exists I. internal(I) & sent(I,S)

## with this derived relation, updr manages to find the commented out invariant below
# derived relation R(node):
#   R(N1) <-> (exists N0. sent(N0,N1) & witness_notif(N0,N1) & internal(N0))

# invariant !(exists node1:node. allowed_in(node1) & internal(node1))
# invariant !aborted
# invariant !(exists node0:node, node1:node. !allowed_in(node0) & sent(node0, node1) & !witness_notif(node0, node1))
# invariant !(exists node0:node, node1:node. !allowed_in(node0) & !allowed_in(node1) & sent(node0, node1))
# invariant !(exists node1:node. !R(node1) & allowed_in(node1))
