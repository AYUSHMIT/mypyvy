sort key
sort value
sort node

mutable relation table(node, key, value)
mutable relation owner(node, key)
mutable relation transfer_msg(node, key, value)

mutable relation active_node(node)
mutable relation active_key(key)
mutable relation active_value(value)

init active_node(N)
init active_key(K)
init active_value(V)

init !table(N, K, V)
init !transfer_msg(N, K, V)
init owner(N1, K) & owner(N2, K) -> N1 = N2
init forall K. exists N. owner(N, K)

transition reshard(k: key, v: value, n_old: node, n_new: node)
  modifies table, owner, transfer_msg
  table(n_old, k, v) &
  active_key(k) & active_value(v) & active_node(n_old) & active_node(n_new) &
  (new(table(N, K, V)) <-> table(N, K, V) & !(N = n_old & K = k & V = v)) &
  (new(owner(N, K)) <-> owner(N, K) & !(N = n_old & K = k)) &
  (new(transfer_msg(N, K, V)) <-> transfer_msg(N, K, V) | (N = n_new & K = k & V = v))


transition recv_transfer_msg(n: node, k: key, v: value)
  modifies table, owner, transfer_msg
  transfer_msg(n, k, v) &
  active_key(k) & active_value(v) & active_node(n) &
  (new(transfer_msg(N, K, V)) <-> transfer_msg(N, K, V) & !(N = n & K = k & V = v)) &
  (new(table(N, K, V)) <-> table(N, K, V) | (N = n & K = k & V = v)) &
  (new(owner(N, K)) <-> owner(N, K) | (N = n & K = k))


transition put(n: node, k: key, v: value)
  modifies table
  owner(n, k) &
  active_key(k) & active_value(v) & active_node(n) &
  (!(N = n & K = k) -> (new(table(N, K, V)) <-> table(N, K, V))) &
  (new(table(n, k, V)) <-> V = v)


transition decrease_domain()
  modifies active_node, active_key, active_value
  (forall N. new(active_node(N)) -> active_node(N)) &
  (forall K. new(active_key(K)) -> active_key(K)) &
  (forall V. new(active_value(V)) -> active_value(V))

sat trace {
  decrease_domain
  assert !((exists N,K,V. active_node(N) & active_key(K) & active_value(V) & transfer_msg(N,K,V)) |
           (forall K. active_key(K) -> exists N. active_node(N) & owner(N,K)))
}
