# Model of a leader election in a ring protocol, adapted from:
#
# Oded Padon, Kenneth McMillan, Aurojit Panda, Mooly Sagiv, Sharon
# Shoham. Ivy: Safety Verification by Interactive Generalization. PLDI
# 2016. https://doi.org/10.1145/2908080.2908118
#
# This model uses only one sort, for nodes. Compared to ring.pyv, IDs
# are eliminated by using a binary relation between nodes that
# compares their IDs. The safety property is that there is at most one
# leader.

sort node

immutable relation le(node, node)

axiom le(X,X)
axiom le(X, Y) & le(Y, Z) -> le(X, Z)
axiom le(X, Y) & le(Y, X) -> X = Y
axiom le(X, Y) | le(Y, X)

immutable relation btw(node, node, node)

axiom btw(W,X,Y) & btw(W,Y,Z) -> btw(W,X,Z)
axiom btw(W,X,Y) -> !btw(W,Y,X)
axiom btw(W,X,Y) | btw(W,Y,X) | W=X | W=Y | X=Y
axiom btw(X,Y,Z) -> btw(Y,Z,X)

mutable relation leader(node)
mutable relation pending(node, node)

init !leader(N)
init !pending(N1, N2)

transition send(n: node, next: node)
  modifies pending
  (forall Z. n != next & ((Z != n & Z != next) -> btw(n,next,Z))) &
  (forall N1, N2. new(pending(N1, N2)) <-> pending(N1, N2) | N1 = n & N2 = next)

transition recv(sender: node, n: node, next: node)
  modifies leader, pending
  (forall Z. n != next & ((Z != n & Z != next) -> btw(n,next,Z))) &
  pending(sender, n) &
  if sender = n
  then
    (forall N. new(leader(N)) <-> leader(N) | N = n) &
    (forall N1, N2.
      !(N1 = sender & N2 = n) ->  # message may or may not be removed
      (new(pending(N1, N2)) <-> pending(N1, N2)))
  else
    (forall N. new(leader(N)) <-> leader(N)) &
    if le(n, sender)
    then
      forall N1, N2.
        !(N1 = sender & N2 = n) ->  # message may or may not be removed
        (new(pending(N1, N2)) <-> pending(N1, N2) | N1 = sender & N2 = next)
    else
      forall N1, N2.
        !(N1 = sender & N2 = n) ->  # message may or may not be removed
        (new(pending(N1, N2)) <-> pending(N1, N2))

safety [leader_unique] leader(X) & leader(Y) -> X = Y

invariant [leader_max] leader(L) -> le(N, L)
invariant [seflf_pending_max] pending(L, L) -> le(N, L)
invariant [no_bypass] pending(S, D) & btw(S, N, D) -> le(N, S)

sat trace {
  any transition
  any transition
  any transition
  assert exists L. leader(L)
}
