# Simple model of sharded key-value store without retransmission, from:
#
# Yotam M. Y. Feldman, James R. Wilcox, Sharon Shoham, and Mooly
# Sagiv. Inferring Inductive Invariants from Phase Structures. CAV
# 2019. https://doi.org/10.1007/978-3-030-25543-5_23
#
# Inspired by the sharded key value store from:
#
# Chris Hawblitzel, Jon Howell, Manos Kapritsos, Jacob R. Lorch, Bryan
# Parno, Michael L. Roberts, Srinath Setty, and Brian Zill. IronFleet:
# Proving Practical Distributed Systems Correct. SOSP 2015.
# https://doi.org/10.1145/2815400.2815428
#
# This file does not have message retransmission, which greatly
# simplifies the protocol. The safety property here is that keys are
# not lost. This is a slightly contrived property, but it's useful as
# a simple problem for finding non-universal invariants. The safety
# property is a global property across all keys that either every key
# is owned by a node or at least one key is being transferred, while
# the inductive invariant needs to say that every key is locally
# either being transferred or owned by a node.

sort key
sort node
sort value

mutable relation table(node, key, value)
mutable relation owner(node, key)
mutable relation transfer_msg(node, key, value)

init !table(N, K, V)
init !transfer_msg(N, K, V)
init owner(N1, K) & owner(N2, K) -> N1 = N2
init forall K. exists N. owner(N, K)

transition reshard(k: key, v: value, n_old: node, n_new: node)
  modifies table, owner, transfer_msg
  table(n_old, k, v) &
  (new(table(N, K, V)) <-> table(N, K, V) & !(N = n_old & K = k & V = v)) &
  (new(owner(N, K)) <-> owner(N, K) & !(N = n_old & K = k)) &
  (new(transfer_msg(N, K, V)) <-> transfer_msg(N, K, V) | (N = n_new & K = k & V = v))


transition recv_transfer_msg(n: node, k: key, v: value)
  modifies table, owner, transfer_msg
  transfer_msg(n, k, v) &
  (new(transfer_msg(N, K, V)) <-> transfer_msg(N, K, V) & !(N = n & K = k & V = v)) &
  (new(table(N, K, V)) <-> table(N, K, V) | (N = n & K = k & V = v)) &
  (new(owner(N, K)) <-> owner(N, K) | (N = n & K = k))


transition put(n: node, k: key, v: value)
  modifies table
  owner(n, k) &
  (!(N = n & K = k) -> (new(table(N, K, V)) <-> table(N, K, V))) &
  (new(table(n, k, V)) <-> V = v)

safety [no_lost_keys] (exists N,K,V. transfer_msg(N,K,V)) | (forall K. exists N. owner(N,K))

invariant forall K. (exists N. owner(N, K)) | (exists N, V. transfer_msg(N, K, V))

sat trace {
  put
  reshard
  recv_transfer_msg
  put
  assert forall X, Y: node. X = Y
}
